0	POKE 56, 32 : POKE 52, 32 : CLR : REM PROTECT SCREEN FROM BASIC			:rem 108
1	POKE 53280, 1 : PRINT "{CLR}{WHT}" : GOTO 100						:rem 102
2	GOSUB 26 : BASE = 2 * 4096 : REM START ADDRESS OF HIRES SCREEN			:rem 93
3	POKE 53272, PEEK(53272) OR 8 : REM BIT MAP AT 8192					:rem 39
4	POKE 53265, PEEK(53265)OR 32 : REM BIT MAP ON						:rem 141
5	SYS 49152 : REM CLR HIRES SCREEN								:rem 115
6	SYS 49173 : REM SET SCREEN COLOR (BITS THAT ARE OFF)				:rem 237
7	X = 160 : Y = 100 : REM X & Y START POSITIONS						:rem 15
8	GO SUB 13 : REM READ JOYSTICK								:rem 198
9	REM UPDATE SCREEN											:rem 160
10	CH = INT(X/8) : RO = INT(Y/8) : LN = YAND7 : BY = BA SE + RO * 320 + 8 * CH + LN : BI = 7 - (X AND 7)	:rem 90
11	POKE BY, PEEK(BY) OR (2â†‘BI) : GOTO 8							:rem 33
12	REM READ JOYSTICK											:rem 211
13	JV = PEEK(56320) : FR = JV AND 16								:rem 160
15	X = X + ((JV AND 4) = 0) - ((JV AND 8) = 0)						:rem 27
16	Y = Y + ((JV AND 1) = 0) - ((JV AND 2) = 0)						:rem 21
19	IF FR = 0 THEN 5											:rem 98
20	IF X > 319 THEN X = 319										:rem 133
21	IF Y > 199 THEN Y = 199										:rem 148
22	IF X < 0 THEN X = 0										:rem 171
23	IF Y < 0 THEN Y = 0										:rem 174
24	GET A$ : IF A$ < > "Q" THEN RETURN								:rem 247
25	POKE 56, 160 : POKE 52, 160 : POKE 53272, 21 : POKE 53265, 27 : PRINT "{CLR}" : END				:rem 4
26	PRINT "{CLR}" TAB(18)"{DOWN}MENU{DOWN}{4 LEFT}[<4 Y>]"				:rem 72
27	PRINT "{DOWN}" TAB(16) "Q{2 SPACES} - QUIT"						:rem 223
28	PRINT "{DOWN}" TAB(9) "FIRE BUTTON - CLR SCREEN"					:rem 193
29	PRINT "{DOWN}" TAB(10) "JOYSTICK MOVES LINE."						:rem 106
30	PRINT "{3 DOWN}{7 RIGHT} ENTER BORDER COLOR (0 TO 15)." : PRINT SPC(18);	:rem 71
31	INPUT BC : POKE 53280, BC AND 15								:rem 206
32	PRINT"{3 DOWN}{7 RIGHT} ENTER SCREEN COLOR (0 TO 15)." : PRINT SPC(18);	:rem 75
33	INPUT SC : POKE 49174, SC AND 15 : RETURN						:rem 19
94	END : REM ELECTRIC ERASER									:rem 111
95	A = PEEK(61) + 256 * PEEK(62) + 3 : POKE 786, INT(A/256) : POKE 785,A - 256 * PEEK(786)			:rem 3
96	POKE A - 2, 0 : POKE A - 1,0 : POKE 45, PEEK(785) : POKE 46, PEEK(786) : CLR : GOTO95			:rem 44
100	FOR I = 0 TO 42 : READ J : POKE 49152 + I, J : NEXT I : GOTO 2							:rem 150
101	DATA 169, 0, 162, 32, 160, 0, 132, 33, 134, 34, 145, 33, 200, 208, 251, 232, 224, 64, 208, 244	:rem 17
102	DATA 96, 169, 1, 162, 4, 160, 0, 132, 33, 134, 34, 145, 33, 200, 208, 251, 232, 224, 8, 208, 244	:rem 75
103	DATA 96, 0	